#        Analogrechner           Parameter:           (c) IERT  27.03.1997
#     Aktiver Verstaerker        k1 .. Verstaerkung [1]
#     mit drei Eingaenge         k2 .. Verstaerkung [1]
#         _                      k3 .. Verstaerkung [1]
#  u1 ___| |\                    AB .. Anfangsbedingung -y(0) [1]
#        | |  \
#  u2 ___| |    \___ y           ACHTUNG: Dieses Element invertiert!
#        | |    /                      /t
#  u3 ___| |  /                  y = - | (k1*u1 + k2*u2 + k3*u3) dT
#        |_|/ |                        /0
#             |                  Hinweis:
#            AB                  Ein ueblicher Analogrechner erlaubt fuer
#                                k1, k2, k3 nur die Werte 1 oder 10.
#
BLOCK BINT3NEG;
$ BLOCKDIM -1 -1 5 7
$ TEXT "Inv. Integrator"
$ TEXTALIG 1 1 1 1
$ ICONNAME "*** NO PIC ***"
$ ICONDIM 4 2
$ ICONALIG 1 1 1
INPUT
   u1 "[1] Eingang";
   u2 "[1] Eingang";
   u3 "[1] Eingang";
OUTPUT
   y "[1] Ausgang";
PARAMETER
   k1 = 1 "[1] Verstaerkung u1";
   k2 = 1 "[1] Verstaerkung u2";
   k3 = 1 "[1] Verstaerkung u3";
   AB = 0.0 "[1] Anfangsbedingung -y(0)";
STATE
   X "[1] Integrator";
SIM
   X .= -k1*u1 -k2*u2 -k3*u3;
   y = X;
ENDSIM

INIT
   X = -AB;
ENDINIT
ENDBLOCK BINT3NEG;
