#  Polygonzug mit 5 Stuetz-      Parameter:           (c) IERT  01.04.1997
#        punkten                 u1 .. u - Knickpunkt 1 [1]
#                                y1 .. y - Knickpunkt 1 [1]
#  Kennlinie                     u2 .. u - Knickpunkt 2 [1]
#                                y2 .. y - Knickpunkt 2 [1]
#  y ^                           u3 .. u - Knickpunkt 3 [1]
#    |                           y3 .. y - Knickpunkt 3 [1]
#    |      y3 .                 u4 .. u - Knickpunkt 4 [1]
#    |  y2 .      . y4           y4 .. y - Knickpunkt 4 [1]
#    |                . y5       u5 .. u - Knickpunkt 5 [1]
#   y1 .                         y5 .. y - Knickpunkt 5 [1]
#   -+--------------------->
#    0 u1  u2  u3 u4  u5 u       Zwischen den Stuetzpunkten (un/yn) wird linear
#                                interpoliert. Fuer u > u5 wird die Gerade
#                                (u4/y4) - (u5/y5), fuer u < u1 die Gerade
#                                (u1/y1) - (u2/y2) weiterverwendet.
#
BLOCK BPOLYG5;
$ BLOCKDIM -1 -1 7 13
$ TEXT "Polygon 5"
$ TEXTALIG 1 1 1 1
$ ICONNAME "*** NO PIC ***"
$ ICONDIM 4 4
$ ICONALIG 1 1 1
INPUT
   u "[1] Polygon Eingang";
OUTPUT
   y "[1] Polygon Ausgang";
PARAMETER
   u1 = -2.0 "[1]  u - Knickpunkt 1 ";
   y1 =  2.0 "[1]  y - Knickpunkt 1 ";
   u2 = -1.0 "[1]  u - Knickpunkt 2 ";
   y2 =  1.0 "[1]  y - Knickpunkt 2 ";
   u3 =  0.0 "[1]  u - Knickpunkt 3 ";
   y3 =  0.0 "[1]  y - Knickpunkt 3 ";
   u4 =  1.0 "[1]  u - Knickpunkt 4 ";
   y4 =  1.0 "[1]  y - Knickpunkt 4 ";
   u5 =  2.0 "[1]  u - Knickpunkt 5 ";
   y5 =  2.0 "[1]  y - Knickpunkt 5 ";
VAR
   bstate   DISCRETE "Zustand";
SIM
   SWITCH bstate
   CASE 0:  # u zwischen -unendl. und u2
      ONRISE u >  u2 DO
         PROCEDURE set_to_u23;
            bstate = 1;
            STORE ALL;
         ENDPROCEDURE
      y = y1+(u-u1)*((y2-y1)/(u2-u1));
   CASE 1:  # u zwischen u2 und u3
      ONRISE u <= u2 DO
         PROCEDURE;
            bstate = 0;
            STORE ALL;
         ENDPROCEDURE
      ONRISE u >  u3 DO
         PROCEDURE set_to_u34;
            bstate = 2;
            STORE ALL;
         ENDPROCEDURE
      y = y2+(u-u2)*((y3-y2)/(u3-u2));
   CASE 2:  # u zwischen u3 und u4
      ONRISE u <= u3 DO set_to_u23;
      ONRISE u >  u4 DO
         PROCEDURE set_to_u45;
            bstate = 3;
            STORE ALL;
         ENDPROCEDURE
      y = y3+(u-u3)*((y4-y3)/(u4-u3));
   CASE 3:  # u zwischen u4 und + unendl.
      ONRISE u <  u4 DO set_to_u34;
     y = y4+(u-u4)*((y5-y4)/(u5-u4));
   ENDSWITCH
ENDSIM

INIT
   IF u2 <= u1 THEN
      EXIT "u2 darf nicht kleiner oder gleich u1 sein!";
   ENDIF
   IF u3 <= u2 THEN
      EXIT "u3 darf nicht kleiner oder gleich u2 sein!";
   ENDIF
   IF u4 <= u3 THEN
      EXIT "u4 darf nicht kleiner oder gleich u3 sein!";
   ENDIF
   IF u5 <= u4 THEN
      EXIT "u5 darf nicht kleiner oder gleich u4 sein!";
   ENDIF
   bstate = 0;
ENDINIT
ENDBLOCK BPOLYG5;
